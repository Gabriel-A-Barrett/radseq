//
// Align reads w/ BWA or MINIMAP
//

include { BWA_INDEX                            } from '../../modules/nf-core/bwa/index/main.nf'
include { BWA_MEM                              } from '../../modules/local/bwa/mem/main.nf'
include { BWAMEM2_INDEX                        } from '../../modules/nf-core/bwamem2/index/main.nf'
include { BWAMEM2_MEM                          } from '../../modules/local/bwamem2/mem/main.nf'
include { MINIMAP2_INDEX                       } from '../../modules/nf-core/minimap2/index/main.nf'
include { MINIMAP2_ALIGN                       } from '../../modules/nf-core/minimap2/align/main.nf'
include { UMITOOLS_DEDUP                       } from '../../modules/nf-core/umitools/dedup/main.nf'
include { SAMTOOLS_INDEX as SAMTOOLS_INDEX_UMI } from '../../modules/nf-core/samtools/index/main.nf'
include { SAMTOOLS_INDEX as SAMTOOLS_INDEX_BAM } from '../../modules/nf-core/samtools/index/main.nf'
include { BAM_STATS_SAMTOOLS                   } from '../nf-core/bam_stats_samtools/main.nf'
include { BAM_MERGE_INDEX_SAMTOOLS             } from '../nf-core/bam_merge_index_samtools/main.nf'


workflow FASTQ_INDEX_ALIGN_BWA_MINIMAP {

    take:
    reads
    fasta
    fai
    sequence_type
    read_lengths

    main:
    ch_versions = Channel.empty()

    switch ( params.aligner ) {

        case 'bwamem': 

            BWA_INDEX (fasta)
            ch_versions = ch_versions.mix(BWA_INDEX.out.versions)

            // add specific arguments 
            bam = BWA_MEM (reads, BWA_INDEX.out.index.first(), true, sequence_type, read_lengths.collect()).bam
            ch_versions = ch_versions.mix(BWA_MEM.out.versions)
                break
        
        case 'minimap2':

            //MINIMAP_INDEX (fasta)
            //ch_versions = ch_versions.mix(MINIMAP_INDEX.out.versions)

            bam = MINIMAP2_ALIGN (reads, fasta.map{it[1]}.first(), true, false, false).bam
            ch_versions = ch_versions.mix(MINIMAP2_ALIGN.out.versions)
                break

        case 'bwamem2':

            BWAMEM2_INDEX (fasta)
            ch_versions = ch_versions.mix(BWAMEM2_INDEX.out.versions)

            // add specific arguments 
            bam = BWAMEM2_MEM (reads, BWAMEM2_INDEX.out.index.first(), true, sequence_type, read_lengths.collect()).bam
            ch_versions = ch_versions.mix(BWAMEM2_MEM.out.versions)
                break
    }

    // split up into 2 channels to decide input for deduplication
    bam_to_umi = bam
        .branch { meta, bam ->
            umi: meta.umi_barcodes == true
            no_umi: meta.umi_barcodes == false
        }
    // need indices with bam files for umitools dedup
    bam_bai_to_umi = SAMTOOLS_INDEX_UMI(bam_to_umi.umi).bai.join(bam_to_umi.umi).map{meta, bai, bam -> [meta, bam, bai]}
    ch_versions = ch_versions.mix(SAMTOOLS_INDEX_UMI.out.versions)

    // query only reads that satisfied the above condition
    umi_bam = UMITOOLS_DEDUP (bam_bai_to_umi, false).bam
    ch_versions = ch_versions.mix(UMITOOLS_DEDUP.out.versions)

    bai = SAMTOOLS_INDEX_BAM (bam_to_umi.no_umi.mix(umi_bam)).bai
    ch_versions = ch_versions.mix(SAMTOOLS_INDEX_BAM.out.versions)

    // add deduplicated umi reads to no_umi channnel and join the index's
    bam_bai = bam_to_umi.no_umi
        .mix(umi_bam)
        .join(bai)

    // get statistics
    BAM_STATS_SAMTOOLS (bam_bai, fasta.map{it[1]}.first())
    ch_versions = ch_versions.mix(BAM_STATS_SAMTOOLS.out.versions)

    mbam_bai = BAM_MERGE_INDEX_SAMTOOLS (bam_bai.map{meta,bam,bai -> [meta, bam]}, fasta.map{it[1]}.first(), fai.map{it[1]}.first()).bam_bai
    ch_versions = ch_versions.mix(BAM_MERGE_INDEX_SAMTOOLS.out.versions)

    emit:
    bam_bai
    mbam_bai
    stats    = BAM_STATS_SAMTOOLS.out.stats
    flagstat = BAM_STATS_SAMTOOLS.out.flagstat
    idxstats = BAM_STATS_SAMTOOLS.out.idxstats
    
    versions = ch_versions
}